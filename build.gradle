plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id "org.openapi.generator" version "7.7.0"
}

group = 'com.ama'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0"
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec.set(file("${projectDir}/src/main/resources/static/openapi.json").toURI().toString())
    outputDir = "$buildDir/generated"
    apiPackage = "com.ama.openapigenerator.api"
    modelPackage = "com.ama.openapigenerator.dto"
    modelNameSuffix.set("Dto")
    groupId.set("com.ama.openapigenerator")
    configOptions = [
            library: 'spring-boot',
            dateLibrary: 'java8',
            interfaceOnly: 'true',
            skipDefaultInterface: 'true',
            useTags: 'true',
            useSpringBoot3: 'true',
    ]
}

sourceSets {
    main {
        java {
            srcDirs "$buildDir/generated/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate

tasks.named('test') {
    useJUnitPlatform()
}

openApiValidate {
    inputSpec.set(file("${projectDir}/src/main/resources/static/openapi.json").toURI().toString())
}


